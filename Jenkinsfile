#!/usr/bin/env groovy

// Version du template Maven : 2.0.6
// Documentation : https://docs.apps.eul.sncf.fr/construire/templates/maven/

// D√©claration de la shared library build_libs
@Library('build_libs') _

// Options du build
Map options = [
    // Nombre de builds √† conserver dans l'historique des builds du job Jenkins
    NUM_TO_KEEP: 10,
    // Branche sur laquelle l'analyse qualit√© et la publication sont applicables
    BRANCH_DEVELOP: 'develop',
    // Version de l'image Maven utilis√©e pour le build
    MAVEN_VERSION: '3.9.6-eclipse-temurin-21-alpine',
    // D√©finir √† true si une image Docker applicative est √† produire
    DOCKER_BUILD: false,
    // D√©finir √† true pour des logs du build Docker plus d√©taill√©s
    IMAGE_BUILD_VERBOSE: false,
    // Chemin du pom maven local au workspace
    POM_PATH: 'pom.xml',
]

// Valorisation des options par d√©faut
options = defaultBuildOptions(options)

// Propri√©t√©s du job
properties([
    // Connexion GitLab
    gitLabConnection("${options['GITLAB_NAME']}"),
    // Conservation des N derni√®res ex√©cutions
    buildDiscarder(logRotator(numToKeepStr: "${options['NUM_TO_KEEP']}")),
    // D√©clenchement du job via webhook lors de push de nouveau code ou de cr√©ation de merge request
    pipelineTriggers([[$class: 'GitLabPushTrigger', triggerOnPush: true, triggerOnMergeRequest: true, branchFilterType: 'All']]),
])

// L'analyse qualit√©, la publication des artefacts et le build Docker sont lanc√©s uniquement sur la branche de d√©veloppement
Boolean qualityAnalysisEnabled = env.BRANCH_NAME == options['BRANCH_DEVELOP']
Boolean publishEnabled = env.BRANCH_NAME == options['BRANCH_DEVELOP']
Boolean dockerEnabled = env.BRANCH_NAME == options['BRANCH_DEVELOP'] && options['DOCKER_BUILD'] == true

// Pour extraction d'informations √† partir du pom du projet
def pom

// Sinon, s'il est activ√©, l'horodatage des logs du CLI eul est d√©cal√© d'une heure
env.TZ = 'Europe/Paris'

// Horodatage des lignes de log Jenkins
timestamps {
    // Prise en charge des codes ANSI pour affichage de logs Jenkins coloris√©s
    ansiColor('xterm') {
        withTools([
            [name: 'maven', version: "${options['MAVEN_VERSION']}"],
            dockerEnabled ? [name: 'buildkit', image: 'moby/buildkit', version: 'rootless'] : [:],
        ]) {
            try {
                stage('Checkout') {
                    println 'üî∞ R√©cup√©ration du code source'
                    scmInfo = checkout scm
                    env.GIT_URL = scmInfo.GIT_URL
                    env.GIT_COMMIT = scmInfo.GIT_COMMIT
                    println '‚úîÔ∏è R√©cup√©ration du code source effectu√©e'
                }
                stage('Setup') {
                    println 'üî∞ Configuration du build'
                    // Extraction de la version √† partir du pom
                    pom = readMavenPom file: options['POM_PATH']
                    options['VERSION'] = pom.version
                    println '‚úîÔ∏èÔ∏è Configuration du build effectu√©e'
                }
                stage('Build') {
                    container('maven') {
                        println 'üî∞ Compilation'
                        sh 'eul mvn compile -Dmaven.test.skip'
                        println '‚úîÔ∏è Build effectu√©'
                    }
                }
                stage('Tests') {
                    container('maven') {
                        println 'üî∞ Ex√©cution des tests unitaires'
                        if (sh(script: 'eul mvn org.jacoco:jacoco-maven-plugin:prepare-agent test org.jacoco:jacoco-maven-plugin:report', returnStatus: true)) {
                            unstable '‚ö†Ô∏èÔ∏è Les tests unitaires ont √©chou√©'
                        }
                        println '‚úîÔ∏è Ex√©cution des tests unitaires effectu√©e'
                    }
                }
                stage('QA') {
                    container('maven') {
                        when(qualityAnalysisEnabled) {
                            println 'üî∞ Analyse qualit√©'
                            String sonarBranch = env.BRANCH_NAME?.replaceAll(/[\\]/, '_').replaceAll(/[, ]/, '')
                            String sonarProjectName = "${pom.name} ${sonarBranch}".trim()
                            withSonarQubeEnv('sonarqube') {
                                sh """\
                                    eul mvn sonar:sonar \
                                        -DskipTests=true \
                                        -Dsonar.projectKey=${options['PROJECT_SONAR_KEY_BRANCH']} \
                                        -Dsonar.projectName="${sonarProjectName}" \
                                        -Dsonar.links.ci=${JOB_URL} \
                                        -Dsonar.links.homepage=${GIT_URL}
                                """
                            }
                            println '‚úîÔ∏è Analyse qualit√© effectu√©e'
                        }
                    }
                }
                stage('Local CVE scan') {
                    container('maven') {
                        println 'üî∞ Scan de s√©curit√© local sur les artefacts produits'
                        // Scan CVE d√©clench√© au plus t√¥t pour ne pas publier d'artefact avec vuln√©rabilit√©s critiques
                        if (sh(script: 'eul artefacts cve-scan --local --critical --mvn', returnStatus: true)) {
                            // La pr√©sence de CVE critiques dans les artefacts est bloquante
                            error "‚ùå Le scan de CVE a identifi√© des vuln√©rabilit√©s critiques dans l'artefact ou ses d√©pendances"
                        }
                        println '‚úîÔ∏èÔ∏è Scan CVE local effectu√©'
                    }
                }
                stage('Publish') {
                    when(publishEnabled) {
                        container('maven') {
                            println "üî∞ Publication des artefacts du projet dans Artifactory depuis la branche ${BRANCH_NAME}"
                            // Publication des artefacts dans Artifactory
                            sh 'eul mvn install -DskipTests=true'
                            // Publication des informations de build dans Artifactory
                            sh 'eul artefacts build-publish'
                            // Publication Jenkins du lien sur le build Artifactory
                            publishArtifactoryBuildLink()
                            println '‚úîÔ∏è Publication des artefacts effectu√©e'
                        }
                    }
                }
                stage('CVE scan') {
                    when(publishEnabled) {
                        container('maven') {
                            println 'üî∞ Scan CVE distant sur les artefacts produits sur Artifactory'
                            // Pour des raisons de performances, l'√©xecution du scan de build est asynchrone. C'est √† dire, le r√©sultat final du scan n'est pas attendu par le pipeline.
                            // Le lien artifactory vers le scan de build est publi√© directement dans le job et accessible.
                            // Pour forcer le pipeline √† attendre le r√©sultat du scan de build, il faut rajouter le flag `--wait-for-result` √† la commande de scan cve comme suit:
                            // <eul artefacts cve-scan --wait-for-result>
                            // cf: https://docs.apps.eul.sncf.fr/share/securise-scan-dependances
                            if (sh(script: 'eul artefacts cve-scan', returnStatus: true)) {
                                // Si le pipeline arrive √† ce stage cela veut dire qu'il a pass√© le scan local et ne pr√©sente pas de CVE critiques mais hautes.
                                // Pour mettre la pipeline en erreur, remplacer unstable "‚ö†Ô∏è..." par error "‚ùå..." dans la ligne suivante
                                unstable "‚ö†Ô∏èÔ∏è Le scan de CVE a identifi√© des vuln√©rabilit√©s hautes dans l'artefact ou ses d√©pendances"
                            }
                            println '‚úîÔ∏è Scan CVE distant effectu√©'
                        }
                    }
                }
                stage('Docker build') {
                    when(dockerEnabled) {
                        container('buildkit') {
                            println 'üî∞ build Docker avec Buildkit'
                            String dockerVerboseOption = options['IMAGE_BUILD_VERBOSE'] ? '--debug' : ''
                            env.IMAGE_NAME = options['PROJECT_NAME']
                            env.IMAGE_VERSION = options['VERSION']
                            result = sh(script: "eul image $dockerVerboseOption build docker --tag=$IMAGE_NAME:$IMAGE_VERSION --build-arg 'VERSION=$IMAGE_VERSION'", returnStatus: true)
                            if (result != 0) {
                                error '‚ùå Le build Docker avec BuildKit a √©chou√©'
                            }
                            println '‚úîÔ∏è build Docker effectu√©'
                        }
                    }
                }

                stage('Docker CVE scan') {
                    when(dockerEnabled) {
                        container('buildkit') {
                            println 'üî∞ Scan CVE distant sur l\'image docker produite'
                            // L'√©xecution du scan docker est synchrone. C'est √† dire, le r√©sultat final du scan est attendu par le pipeline.
                            // Pour forcer le pipeline √† ne pas attendre le r√©sultat du scan docker, il faut enlever le flag `--wait-for-result` √† la commande de scan comme suit:
                            // <eul image scan>
                            // cf: https://docs.apps.eul.sncf.fr/share/scan-conteneur
                            result = sh(script: "eul image scan $IMAGE_NAME:$IMAGE_VERSION --wait-for-result", returnStatus: true)
                            if (result > 0) {
                                if (result == 4) {
                                    // La pr√©sence de CVE critiques dans l'image docker n'est pas bloquante.
                                    // Pour mettre le pipeline en erreur, remplacer unstable "‚ö†Ô∏è..." par error "‚ùå..." dans la ligne suivante.
                                    unstable "‚ö†Ô∏èÔ∏è Le scan CVE distant Docker a identifi√© des vuln√©rabilit√©s critiques dans l'image produite"
                                } else {
                                    error '‚ùå Le Scan CVE distant Docker a √©chou√©'
                                }
                            }

                            println '‚úîÔ∏è Scan CVE distant sur l\'image docker produite effectu√©'
                        }
                    }
                }

                println 'üëç Build du job de snapshot termin√© avec succ√®s'
                updateGitlabCommitStatus name: 'build', state: 'success'
                currentBuild.result = 'SUCCESS'
            } catch (all) {
                updateGitlabCommitStatus name: 'build', state: 'failed'
                currentBuild.result = 'FAILURE'
                // Envoi d'un mail en cas d'√©chec
                emailext(
                    body: '$DEFAULT_CONTENT',
                    subject: '$DEFAULT_SUBJECT',
                    // Destinataires : auteurs d'une modification du code et d√©clencheur du pipeline
                    recipientProviders: [[$class: 'DevelopersRecipientProvider'], [$class: 'RequesterRecipientProvider']],
                    // Pour √©galement envoyer le mail √† une liste d'adresses (s√©par√©es par des ,) :
                    //to: "adressmail1, adressmail2"
                )
                throw all
            } finally {
                // Collecte et remont√©e dans Jenkins des probl√®mes rencontr√©s pendant le build, ainsi que les r√©sultats des tests unitaires.
                // Documentation : https://docs.apps.eul.sncf.fr/share/sharedlib-addngissuesreporttojob/
                addNgIssuesReportToJob(
                    'tools': [
                        'cveScan',
                        'maven',
                        'taskScanner',
                        ['junitPlugin': ['testResults': '**/target/surefire-reports/*.xml']],
                        ['junitParser': ['pattern': '**/target/surefire-reports/*.xml']],
                    ]
                )
                // Pour le suivi et les indicateurs eUL
                eulNotify()
            }
        }
    }
}
